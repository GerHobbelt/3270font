#!/usr/bin/env fontforge

Open("3270_HQ.sfd")
# Force fixed width
SelectAll()
SetWidth(1080)

# Remove IP encumbered characters
Select("uniE0FF")
SelectMore("uniF0FF")
DetachAndRemoveGlyphs()

# Generate unencumbered version for distribution
Save("build/3270_HQ.sfd")

# Remove UC 13 running man and pointing finger building helpers that are too wide
Select("full_running_man")
SelectMore("full_pointing_finger")
DetachAndRemoveGlyphs()
Reencode("compacted")

# From https://fontforge.org/docs/scripting/scripting-alpha.html#Generate
# fmflags&2 => generate a pfm file
# fmflags&0x80 => generate tables so the font will work on both Apple and MS platforms.
# fmflags&0x100 => generate a glyph map file (GID=>glyph name, unicode map). The map file will have extension “.g2n”.
# fmflags&0x200000 => round postscript coordinates
Generate("build/3270-Regular.otf", "", 0x200180)
Generate("build/3270-Regular.ttf")
Generate("build/3270-Regular.pfm", "", 0x2)
Generate("build/3270-Regular.woff")
Generate("build/3270-Regular.svg")

SelectAll()
Scale(91, 100) # Scales the glyphs to 91% of 1080 = 984
SetWidth(984)
SetFontNames("3270SemiCondensed", "IBM 3270 Semi-Condensed", "IBM 3270 Semi-Condensed")
Generate("build/3270SemiCondensed-Regular.otf", "", 0x200180)
Generate("build/3270SemiCondensed-Regular.ttf")
Generate("build/3270SemiCondensed-Regular.pfm", "", 0x2)
Generate("build/3270SemiCondensed-Regular.woff")
Generate("build/3270SemiCondensed-Regular.svg")
Save("build/3270_HQ_SemiCondensed.sfd")

SelectAll()
Scale(99.187, 100) # 99.187% of 984 = 976
SetWidth(976)
SetFontNames("3270Condensed", "IBM 3270 Condensed", "IBM 3270 Condensed")
SetMacStyle("Condensed")
Generate("build/3270Condensed-Regular.otf", "", 0x80)
Generate("build/3270Condensed-Regular.ttf")
Generate("build/3270Condensed-Regular.pfm", "", 0x2)
Generate("build/3270Condensed-Regular.woff")
Generate("build/3270Condensed-Regular.svg")
Save("build/3270_HQ_Condensed.sfd")
